plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.graalvm.buildtools.native' version '0.10.3'
	id("org.openapi.generator") version "7.10.0"
	id "org.sonarqube" version "6.0.0.5145"
}

group = 'it.gov.pagopa'
version = project.version

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudAzureVersion', "5.18.0")
	set('otelInstrumentationVersion','2.10.0')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'

//   spring security + oauth2 resource server
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.security:spring-security-oauth2-jose'

//	implementation 'com.azure.spring:spring-cloud-azure-starter-actuator'
	implementation("io.swagger.core.v3:swagger-annotations:2.2.8")
	implementation("org.openapitools:jackson-databind-nullable:0.2.6")
	implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
	implementation("org.springframework.boot:spring-boot-starter-validation")

	// opentelemetry
	// implementation("io.opentelemetry.instrumentation:opentelemetry-spring-boot-starter")
	// provides the same functionality of spring-boot-starter but configure the azure exporter
	implementation("com.azure.spring:spring-cloud-azure-starter-monitor:1.0.0-beta.6")

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	annotationProcessor("org.projectlombok:lombok")
	compileOnly("org.projectlombok:lombok")
}

dependencyManagement {
	imports {
		mavenBom "com.azure.spring:spring-cloud-azure-dependencies:${springCloudAzureVersion}"
		mavenBom "io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:${otelInstrumentationVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.compileJava {
	dependsOn("openApiGenerate")
}

sourceSets {
    main {
        java {
            srcDir("$projectDir/build/generated/src/main/java")
        }
    }
}

openApiGenerate {
	generatorName.set("spring")
	inputSpec.set("$rootDir/openapi/activation.openapi.yaml")
	outputDir.set("$projectDir/build/generated")
	apiPackage.set("it.gov.pagopa.rtp.activator.controller.generated")
	modelPackage.set("it.gov.pagopa.rtp.activator.model.generated")
	modelNameSuffix.set("Dto")
	generateApiTests.set(false)
	generateApiDocumentation.set(false)
	generateApiTests.set(false)
	generateModelTests.set(false)
	library.set("spring-boot")
    configOptions.set([
        "dateLibrary"                         : "java8",
        "useSpringBoot3"                      : "true",
        "interfaceOnly"                       : "true",
        "useTags"                             : "true",
		"useSwaggerUI"                        : "false",
		"reactive"							  : "true",
		"swaggerAnnotations"				  : "false",
		"skipDefaultInterface"				  : "true",
		"openApiNullable" 					  : "true",
    ])
    typeMappings.set([
        "DateTime"        : "java.time.LocalDateTime",
        "zoned-date-time" : "java.time.ZonedDateTime"
    ])
}


graalvmNative {
	binaries {
		main {
			buildArgs.add("--initialize-at-build-time=org.slf4j.helpers")
			jvmArgs.add("-Djava.security.properties=${file("${projectDir}/src/main/resources/custom.security").absolutePath}")
		}
	}
}

bootRun {
	jvmArgs = [
			'-Djava.security.properties=src/main/resources/custom.security'
	]
}

tasks.withType(JavaExec) {
	jvmArgs += '-Djava.security.properties=src/main/resources/custom.security'
}


test {
	finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
	}
}

sonarqube {
	properties {
		property 'sonar.host.url', 'https://sonarcloud.io'
		property 'sonar.organization', 'pagopa'
		property 'sonar.projectKey', 'pagopa_rtp-activator'
	}
}

